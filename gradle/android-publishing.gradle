apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"

group = libraryGroup
version = libraryVersion

project.tasks.dokkaJavadoc.configure {
    outputDirectory.set(new File("javadoc", project.buildDir))
    moduleName = rootProject.name
}

task dokkaJar(type: Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Assembles Kotlin docs with Dokka"
    archiveClassifier.set("javadoc")
    from(tasks.dokkaJavadoc)
    dependsOn(tasks.dokkaJavadoc)
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId = project.name
                artifact dokkaJar
                artifact sourcesJar
            }
        }
    }
}
